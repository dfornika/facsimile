prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix cfa: <http://purl.org/facsimile/cfa#>
prefix datamodel: <http://purl.org/facsimile/datamodel#>
prefix dbq: <http://purl.org/facsimile/dbq#>

select ?patient ?physician where {
  # get patient names
  ?pat a datamodel:SubjectInformation .
  ?pat cfa:hasPatientName ?patient .
  
  # get patient FormData instance
  ?pat cfa:isComponentOf ?fd .
  ?fd a datamodel:FormData .
       
  # find a 'Yes' answer to Q14A
  ?fd cfa:hasComponent ?comp . 
  ?z cfa:isComponentOf ?comp . 
  ?z datamodel:isDerivedFrom dbq:DBQ_Back_Q14A .
  ?z cfa:hasValue cfa:Yes 
    
  OPTIONAL {
    # get physician name
    ?pd a datamodel:EvaluatorInformation .
    ?pd cfa:isComponentOf ?fd .
    ?pd cfa:hasPhysicianName ?physician
  }
}